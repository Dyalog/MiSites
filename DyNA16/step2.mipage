:Class Step2 : MiPage

    ∇ Compose
      :Access public
⍝ first lay out the order form and cart panels
      Order←⍬
      orderForm←New #._.Form
     
      (orderControls←New _.StackPanel).Horizontal←1
     
      cartPanel←New _.StackPanel orderControls('orderSummary'New _.div)
      ('orderPage'Add _.StackPanel orderForm cartPanel).Horizontal←1
     
⍝ then within the order form, lay out the divisions for burgers, sides, and drinks
      orderPanel←orderForm.Add _.StackPanel
     
⍝ burgers have sub panels for size, bun type, and toppings
      burgerPanel←'burgerFieldSet'orderPanel.Add _.Fieldset'Burgers'
     
      sizePanel←'.subFieldSet'burgerPanel.Add _.Fieldset'Size'
      sizePanel.Add 3 formatInputs'size'∘('radio'formatMenuItem)¨#.BusinessLogic.Menu.burgers.size
     
      bunPanel←'.subFieldSet'burgerPanel.Add _.Fieldset'Bun Type'
      bunPanel.Add 2 formatInputs'bun'∘('radio'formatMenuItem)¨#.BusinessLogic.Menu.burgers.bun
     
      toppingsPanel←'.subFieldSet'burgerPanel.Add _.Fieldset'Toppings'
      toppingsPanel.Add 3 formatInputs'toppings'∘('checkbox'formatMenuItem)¨#.BusinessLogic.Menu.burgers.toppings
     
⍝ add the panel for sides
      sidesPanel←orderPanel.Add _.Fieldset'Sides'
      sidesPanel.Add 2 formatInputs'sides'∘('checkbox'formatMenuItem)¨#.BusinessLogic.Menu.sides
     
⍝ add the panel for drinks
      drinksPanel←orderPanel.Add _.Fieldset'Drinks'
      drinksPanel.Add 3 formatInputs'drinks'∘('checkbox'formatMenuItem)¨#.BusinessLogic.Menu.drinks
     
⍝ add a div with the subtotal and buttons to add the subtotal to the order or reset the panel
     
      (subOrder←'subOrder'orderPanel.Add _.StackPanel).Horizontal←1
      'subTotalPanel'subOrder.Add'Subtotal: '('subtotal'New(_.span(fmtCost SubTotal)))
      ('buttons'subOrder.Add _.div).Add(('add' 'disabled')'reset'){⍺ New _.Button ⍵}¨'Add To Order' 'Reset'
     
⍝ add order buttons and total display area
      orderControls.Add(('order' 'disabled')'cancel'){⍺ New _.Button ⍵}¨'Place Order' 'Cancel Order'
      'orderTotal'orderControls.Add _.div
     
      thankYou←'thankYouPage'Add _.div''
      thankYou.style←'display:none;'
      thankYou.Add _.h3'Thank you for your order!'
      thankYou.Add _.h4'Your order number is '('orderNumber'New _.span)
      'again'thankYou.Add _.Button'Continue'
     
      Add _.Handler'input,button' 'click select' 'checkPage'
    ∇

    ∇ r←checkPage;size;bun;toppings;subOrder;pageData
      :Access public
      r←''
     
      :If 0∊⍴pageData←{0::'' ⋄ ⍵{⍺⍎⍵}¨⍵.⎕NL-2}_PageData
          subOrder←0 3⍴'' '' 0
      :Else
          subOrder←#.BusinessLogic.FoodItems{(⍺⍪'' '' 0)[⍺[;1]⍳⍵;]}⊃,/eis¨pageData
      :EndIf
     
      :Select _what
      :Case 'add'
          Order,←⊂subOrder
          orderTable←#.BusinessLogic.FormatOrder Order
          r,←'#orderSummary'Replace('orderTable'New _.Table(('Qty' 'Item' 'Cost'⍪{⍵[;1 2],fmtCost¨⍵[;3]}orderTable)'' 1))
          r,←Execute'input'_JSS.RemoveAttr'checked'
          r,←'#orderTotal'Replace'Order Total: ',fmtCost+/orderTable[;3]
          r,←Execute'#order'_JSS.Prop'disabled'_false
          r,←Execute'#add'_JSS.Prop'disabled'_true
          SubTotal←0
     
      :Case 'reset'
          r,←Execute'input'_JSS.RemoveAttr'checked'
          SubTotal←0
     
      :Case 'order'
          r,←'#orderNumber'Replace #.BusinessLogic.PlaceOrder Order
          r,←Execute'#thankYouPage,#orderPage'_JSS.Toggle''
     
      :CaseList 'cancel' 'again'
          r,←Execute'location.reload(true)'
     
      :Else
          size bun toppings←Get'size bun toppings'
          :If 0∊⍴size  ⍝ if no burger size has been selected
          :AndIf ∨/~0∘∊∘⍴¨bun toppings  ⍝ and a bun or toppings have been
              r,←Execute('#',(size←⊃#.BusinessLogic.Menu.burgers.size).id)#._JSS.Prop'checked'_true ⍝ select the first size
              subOrder⍪←size.(id name cost)
          :EndIf
          :If 0∊⍴bun  ⍝ if no burger size has been selected
          :AndIf ∨/~0∘∊∘⍴¨size toppings  ⍝ and a bun or toppings have been
              r,←Execute('#',(bun←⊃#.BusinessLogic.Menu.burgers.bun).id)#._JSS.Prop'checked'_true ⍝ select the first bun
              subOrder⍪←bun.(id name cost)
          :EndIf
          SubTotal←+/subOrder[;3]
          r,←Execute'#add'_JSS.Prop'disabled'_false
      :EndSelect
      r,←'#subtotal'Replace fmtCost SubTotal
    ∇


    ∇ r←inputName(inputType formatMenuItem)item;caption
      caption←item.name
      :If item.cost≠0
          caption,←' (',(9×item.cost>0)↓'subtract ',('-'~⍨fmtCost item.cost),')'
      :EndIf
      r←(item.id('name'inputName))New _.Input inputType item.id caption'right'
    ∇

    ∇ r←columns formatInputs inputs
      :Access public
      r←New _.Table(((⌈(⍴inputs)÷columns),columns)⍴inputs,columns⍴⊂'')
    ∇

⍝    ∇r←formatOrderItem orderItem



:EndClass