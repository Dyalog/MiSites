:Class index : bbbPage

    :field public newdate←⍬

    ∇ Compose;dates
      :Access public
      dates←{⍵[⍒⍵]}∊{⊃//⎕VFI 8↑6↓⍵}¨1⌷[2]'Orders*.dcf'#.Files.List #.BusinessLogic.DataPath
      lastdate←0 100 100⊤⊃dates/⍨dates<100⊥3↑⎕TS
      datePicker←Add _.ejDatePicker lastdate('yyyyMMdd')
      datePicker.Label←'Select Log File Date: '
      datePicker.Options.minDate←'⍎',_JSS.JSDate 0 100 100⊤⊃¯1↑dates
      datePicker.Options.maxDate←'⍎',_JSS.JSDate 3↑⎕TS
      datePicker.On'change' 'ChangeDate'('newdate' 'argument' 'value')
     
      :If ~0∊⍴newdate
      :AndIf dates∊⍨newdate←#.Strings.tonum newdate
          date←0 100 100⊤⊃newdate
      :Else
          date←lastdate
      :EndIf
     
      :If 0∊⍴Data←#.BusinessLogic.TwentyMinuteProfile #.BusinessLogic.GetOrdersData date
          Add _.h3'No Data For: ',⍕date
          →0
      :EndIf
     
      Add _.Script('x' 'y'('salesData'_JSS.JSData)Data[;1 3])
      Add _.Script('x' 'y'('orderData'_JSS.JSData)Data[;1 2])
      chart←'perfChart'Add _.ejChart
      chart.Options←('∇date∇'(1 #.Dates.DateFormat date))Subst ScriptFollows
⍝ {
⍝   title:{text:"Sales Performance",subTitle:{text:"For ∇date∇",textAlignment:"center"}},
⍝   primaryXAxis:{title:{text:"Time Slot"}},
⍝   primaryYAxis:{title:{text:"Sales"},labelFormat:"${value}"},
⍝   chartArea:{background:"white"},
⍝   axes:[{
⍝          orientation:"Vertical",
⍝          opposedPosition:true,
⍝          name:"yAxis",
⍝          title:{text:"Orders"},
⍝          axisline:{visible:false},
⍝          majorGridLines:{visible:false}
⍝        }],
⍝   commonSeriesOptions:{tooltip:{visible:true}},
⍝   series:
⍝   [
⍝   {
⍝     type: "line",
⍝     dataSource: salesData,
⍝     name:"Sales",
⍝     xName:"x",
⍝     yName:"y",
⍝     tooltip:{format:"#point.x#<br/>Sold: #point.y#<br/>"},
⍝     enableAnimation:true
⍝   },
⍝   {
⍝     type: "line",
⍝     dataSource: orderData,
⍝     name:"Orders",
⍝     xName:"x",
⍝     yName:"y",
⍝     yAxisName:"yAxis",
⍝     tooltip:{format:"#point.x#<br/>Orders: #point.y#<br/>"},
⍝     enableAnimation:true
⍝   }
⍝   ]
⍝ }
    ∇

    ∇ r←ChangeDate;date
      :Access public
      date←#.Strings.tonum Get'newdate'
      r←Execute'window.location.href="/admin/?newdate=',(⍕newdate),'"'
    ∇
:EndClass